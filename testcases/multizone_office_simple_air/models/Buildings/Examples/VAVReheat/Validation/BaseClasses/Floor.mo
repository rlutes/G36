within Buildings.Examples.VAVReheat.Validation.BaseClasses;
model Floor
  "Extends Buildings.Examples.VAVReheat.BaseClasses.Floor with CO2 generation from people and CO2 from outside air."
  extends Buildings.Examples.VAVReheat.BaseClasses.Floor(
    nor(use_C_flow=true,
        C_start=fill(400e-6*Modelica.Media.IdealGases.Common.SingleGasesData.CO2.MM
                      /Modelica.Media.IdealGases.Common.SingleGasesData.Air.MM, Medium.nC)),
    eas(use_C_flow=true,
        C_start=fill(400e-6*Modelica.Media.IdealGases.Common.SingleGasesData.CO2.MM
                      /Modelica.Media.IdealGases.Common.SingleGasesData.Air.MM, Medium.nC)),
    cor(use_C_flow=true,
        C_start=fill(400e-6*Modelica.Media.IdealGases.Common.SingleGasesData.CO2.MM
                      /Modelica.Media.IdealGases.Common.SingleGasesData.Air.MM, Medium.nC)),
    wes(use_C_flow=true,
        C_start=fill(400e-6*Modelica.Media.IdealGases.Common.SingleGasesData.CO2.MM
                      /Modelica.Media.IdealGases.Common.SingleGasesData.Air.MM, Medium.nC)),
    sou(use_C_flow=true,
      frad=0.2,
        C_start=fill(400e-6*Modelica.Media.IdealGases.Common.SingleGasesData.CO2.MM
                      /Modelica.Media.IdealGases.Common.SingleGasesData.Air.MM, Medium.nC)),
    leaSou(amb(C=fill(400e-6*Modelica.Media.IdealGases.Common.SingleGasesData.CO2.MM
                      /Modelica.Media.IdealGases.Common.SingleGasesData.Air.MM, Medium.nC))),
    leaEas(amb(C=fill(400e-6*Modelica.Media.IdealGases.Common.SingleGasesData.CO2.MM
                      /Modelica.Media.IdealGases.Common.SingleGasesData.Air.MM, Medium.nC))),
    leaNor(amb(C=fill(400e-6*Modelica.Media.IdealGases.Common.SingleGasesData.CO2.MM
                      /Modelica.Media.IdealGases.Common.SingleGasesData.Air.MM, Medium.nC))),
    leaWes(amb(C=fill(400e-6*Modelica.Media.IdealGases.Common.SingleGasesData.CO2.MM
                      /Modelica.Media.IdealGases.Common.SingleGasesData.Air.MM, Medium.nC))));

  Modelica.Blocks.Sources.RealExpression CO2GenWes(y=gai.y[3]*AFloWes/80*
      8.64e-6) "CO2 generated by people in the west zone"
  annotation (Placement(transformation(extent={{-110,50},{-90,70}})));
  Modelica.Blocks.Sources.RealExpression CO2GenCor(y=gai.y[3]*AFloCor/80*
      8.64e-6) "CO2 generated by people in the corridor zone"
  annotation (Placement(transformation(extent={{82,56},{102,76}})));
  Modelica.Blocks.Sources.RealExpression CO2GenEas(y=gai.y[3]*AFloEas/80*
      8.64e-6) "CO2 generated by people in the east zone"
  annotation (Placement(transformation(extent={{254,64},{274,84}})));
  Modelica.Blocks.Sources.RealExpression CO2GenNor(y=gaiIntNor[3].y*AFloNor/80*
      8.64e-6) "CO2 generated by people in the north zone"
  annotation (Placement(transformation(extent={{80,142},{100,162}})));
  Modelica.Blocks.Sources.RealExpression CO2GenSou(y=gaiIntSou[3].y*AFloSou/80*
      8.64e-6) "CO2 generated by people in the south zone"
  annotation (Placement(transformation(extent={{84,-20},{104,0}})));

  Modelica.Blocks.Sources.CombiTimeTable sickPeople(
    table=[0,0; 9,1; 17,0; 33,1; 41,0; 57,1; 65,0; 81,1; 89,0; 105,1; 113,0;
        168,0],
    smoothness=Modelica.Blocks.Types.Smoothness.ConstantSegments,
    timeScale=3600,
    extrapolation=Modelica.Blocks.Types.Extrapolation.Periodic)
    "sick people schedule"
    annotation (Placement(transformation(extent={{-140,-8},{-120,12}})));
  Modelica.Blocks.Math.Gain gaiCov(each k=25/3600) "virus generation rate"
    annotation (Placement(transformation(extent={{-98,-8},{-78,12}})));
equation
  connect(CO2GenWes.y, wes.C_flow[1])
    annotation (Line(points={{-89,60},{10.4,58.8}}, color={0,0,127}));
  connect(CO2GenNor.y, nor.C_flow[1]) annotation (Line(points={{101,152},{132,
          152},{132,138.8},{142.4,138.8}}, color={0,0,127}));
  connect(CO2GenCor.y, cor.C_flow[1]) annotation (Line(points={{103,66},{103,64},
          {132,64},{132,58.8},{142.4,58.8}}, color={0,0,127}));
  connect(CO2GenSou.y, sou.C_flow[1]) annotation (Line(points={{105,-10},{108,
          -10},{142.4,-10},{142.4,-21.2}}, color={0,0,127}));
  connect(CO2GenEas.y, eas.C_flow[1]) annotation (Line(points={{275,74},{275,72},
          {302.4,72},{302.4,78.8}}, color={0,0,127}));
  connect(sickPeople.y[1], gaiCov.u)
    annotation (Line(points={{-119,2},{-100,2}}, color={0,0,127}));
  connect(gaiCov.y, wes.C_flow[2]) annotation (Line(points={{-77,2},{-77,0},{
          -60,0},{-60,58.8},{10.4,58.8}}, color={0,0,127}));
  connect(gaiCov.y, sou.C_flow[2]) annotation (Line(points={{-77,2},{-77,0},{4,
          0},{4,-21.2},{142.4,-21.2}}, color={0,0,127}));
  connect(gaiCov.y, cor.C_flow[2]) annotation (Line(points={{-77,2},{-77,0},{
          -60,0},{-60,56},{4,56},{4,28},{36,28},{36,32},{132,32},{132,58.8},{
          142.4,58.8}}, color={0,0,127}));
  connect(gaiCov.y, nor.C_flow[2]) annotation (Line(points={{-77,2},{-77,0},{
          -60,0},{-60,56},{4,56},{4,28},{36,28},{36,32},{132,32},{132,138.8},{
          142.4,138.8}}, color={0,0,127}));
  connect(gaiCov.y, eas.C_flow[2]) annotation (Line(points={{-77,2},{-77,0},{
          -60,0},{-60,56},{4,56},{4,28},{36,28},{36,32},{132,32},{132,104},{288,
          104},{288,72},{296,72},{296,78.8},{302.4,78.8}}, color={0,0,127}));
annotation(Documentation(info="<html>
<p>
This model is used in the model
<a href=\"modelica://Buildings.Examples.VAVReheat.Validation.TraceSubstance\">
Buildings.Examples.VAVReheat.Validation.TraceSubstance</a>.  It extends
<a href=\"modelica://Buildings.Examples.VAVReheat.BaseClasses.Floor\">
Buildings.Examples.VAVReheat.BaseClasses.Floor</a> with CO<sub>2</sub> generation
from people and CO<sub>2</sub> from outside air infiltration.  Calculation of the
generation from people uses the gain schedule for latent load and assumes
80 W per person latent load to calculate the number of people.  Then, it assumes
CO<sub>2</sub> generation is 0.0048 l/s per person (Table 5, Persily and De Jonge 2017)
and density of CO<sub>2</sub> to be 1.8 kg/m<sup>3</sup>, making CO<sub>2</sub> generation equal to
8.64e-6 kg/s per person. Outside air CO<sub>2</sub> concentration is assumed 400 ppm.
</p>
<h4>References</h4>
<p>
Persily, A. and De Jonge, L. (2017). Carbon dioxide generation rates for
building occupants. <i>Indoor Air</i>, 27, 868–879.
https://doi.org/10.1111/ina.12383.
</p>
</html>",revisions="<html>
<ul>
<li>
May 9, 2021, by David Blum:<br/>
First implementation.
</li>
</ul>
</html>"));
end Floor;
